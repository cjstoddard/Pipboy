' Gambas class file

Public SongCounter As Integer
Public SongMaxCounter As Integer
Public SongTitle As String
Private bTime As Boolean


Public Sub Form_Open()

  Dim sFile As Stream
  Dim sLine As String
  Dim Directory As String
  Dim Files As String[]
  Dim hfile As String
    
  STAT_Panel.Show
  INV_Panel.Hide
  DATA_Panel.Hide
  RADIO_Panel.Hide

' Read pipboy.ini file  
  If Exist(Application.Path & "/pipboy.ini") Then 
    sFile = Open Application.Path & "/pipboy.ini" For Input 
    Line Input #sFile, sLine
    Name_Text.Text = sLine
    Line Input #sFile, sLine
    Aim_TextBox.Text = sLine
    Line Input #sFile, sLine
    Sheild_TextBox.Text = sLine
    Line Input #sFile, sLine
    Voltage_TextBox.Text = sLine
    Line Input #sFile, sLine
    Nuclear_TextBox.Text = sLine
   Close #sFile
 Endif 
'----------

' Read inventory.txt file
  If Exist(Application.Path & "/inventory.txt") Then   
    sFile = Open Application.Path & "/inventory.txt" For Input 
    While Not Eof(sFile)
      Line Input #sFile, sLine
      INV_TextArea.Text = INV_TextArea.Text & Chr(10) & sLine
    Wend   
    Close #sFile
  Endif 
'----------

' Setup MP3 Player
  Directory = Application.Path & "/music"
  Files = Dir(Directory, "*")
  For Each hfile In Files
    MPlaylist_ListBox.Add(hfile)
  Next
  SongCounter = 0
  SongMaxCounter = MPlaylist_ListBox.Count
  MPlaylist_ListBox.Index = SongCounter
  SongTitle = MPlaylist_ListBox.Text
  MSong_TextBox.Text = SongTitle
'----------

Finally  
' Setup Clock
  LCDTimer.Delay = 1000
  LCDTimer.Start
  LCDTimer_Timer()
'----------

' Check For internet connection
  PingTimer.Delay = 30000
  PingTimer.Start
  PingTimer_Timer()
'----------

Catch 
  
End

' Top Tab Buttons
Public Sub STAT_Button_Click()

  STAT_Panel.Show
  INV_Panel.Hide
  DATA_Panel.Hide
  RADIO_Panel.Hide

End

Public Sub INV_Button_Click()

  STAT_Panel.Hide
  INV_Panel.Show
  DATA_Panel.Hide
  RADIO_Panel.Hide

End

Public Sub DATA_Button_Click()

  STAT_Panel.Hide
  INV_Panel.Hide
  DATA_Panel.Show
  RADIO_Panel.Hide

End

Public Sub RADIO_Button_Click()

  STAT_Panel.Hide
  INV_Panel.Hide
  DATA_Panel.Hide
  RADIO_Panel.Show 

End

Public Sub Exit_Button_Click()

 Quit 

End
'----------

' Inventory buttons
Public Sub OPEN_Button_Click()

  Dialog.Filter = ["*.txt", "Text Files"]
  If Dialog.OpenFile() Then Return
  INV_TextArea.Text = File.Load(Dialog.Path)
Catch
  Message.Info(Error.Text)

End

Public Sub SAVE_Button_Click()

  Dialog.Filter = ["*.txt", "Text Files"]
  If Dialog.SaveFile() Then Return
  File.Save(Dialog.Path, INV_TextArea.Text)
Catch
  Message.Info(Error.Text)

End
'----------

' Media player buttons
Public Sub MBack_Button_Click()

  RADIO_MediaView.Stop 
  If SongCounter = 0 Then 
    SongCounter = SongMaxCounter - 1
    Else 
      SongCounter = SongCounter - 1
  Endif
  
  MPlaylist_ListBox.Index = SongCounter
  SongTitle = MPlaylist_ListBox.Text
  MSong_TextBox.Text = SongTitle
  RADIO_MediaView.URL = Application.Path & "/music/" & MPlaylist_ListBox.Text 
  RADIO_MediaView.Play()

End

Public Sub MPlay_Button_Click()

  MPlaylist_ListBox.Index = SongCounter
  RADIO_MediaView.URL = Application.Path & "/music/" & MPlaylist_ListBox.Text 
  RADIO_MediaView.Play()
  
End

Public Sub MStop_Button_Click()

  RADIO_MediaView.Stop 

End

Public Sub MPause_Button_Click()

  RADIO_MediaView.Pause

End

Public Sub MNext_Button_Click()

  RADIO_MediaView.Stop 
  SongCounter = SongCounter + 1
  If SongCounter > SongMaxCounter Then 
    SongCounter = 0
  Endif
  
  MPlaylist_ListBox.Index = SongCounter
  SongTitle = MPlaylist_ListBox.Text
  MSong_TextBox.Text = SongTitle
  RADIO_MediaView.URL = Application.Path & "/music/" & MPlaylist_ListBox.Text 
  RADIO_MediaView.Play()

End
'----------

' Handles updating the clock
Public Sub LCDTimer_Timer()

  bTime = Not bTime
  LCDTimeLabel.Text = Format(Now, "hh" & If(bTime, ":", "") & "nn" & If(bTime, ":", "") & "ss")

End  

' Checks for an internet connection

Public Sub PingTimer_Timer()
  
  Dim strResult As String

  Exec ["ping", "-c 1", "-w 1", "8.8.8.8"] To strResult
  If InStr(strResult, "64 bytes from") > 0 Then
    DATA_WebView1.Url = "https://maps.google.com"
    DATA_WebView1.Refresh
  Else
    DATA_WebView1.URL = "file://" & Application.Path & "/map.html "
    DATA_WebView1.Refresh
  Endif

End
